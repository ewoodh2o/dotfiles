;; move-text.el - nifty moving up and down of blocks of text
;; Elliott Wood <elliott.wood@two-fish.com>

(defun move-text-internal (arg)
  (cond
   ((and mark-active transient-mark-mode)
    (if (> (point) (mark))
        (exchange-point-and-mark))
    (let ((column (current-column))
          (text (delete-and-extract-region (point) (mark))))
      (forward-line arg)
      (move-to-column column t)
      (set-mark (point))
      (insert text)
      (exchange-point-and-mark)
      (setq deactivate-mark nil)))
   (t
    (let ((column (current-column)))
      (beginning-of-line)
      (when (or (> arg 0) (not (bobp)))
        (forward-line)
        (when (or (< arg 0) (not (eobp)))
          (transpose-lines arg))
        (forward-line -1))
      (move-to-column column t)))))

(defun move-text-down (arg)
  "Move region (transient-mark-mode active) or current line arg lines down."
  (interactive "*p")
  (move-text-internal arg))

(defun move-text-up (arg)
  "Move region (transient-mark-mode active) or current line arg lines up."
  (interactive "*p")
  (move-text-internal (- arg)))

;; (defun ew-indent()
;;   "*Indent code by two spaces."
;;   (interactive)
;;   (indent-rigidly (region-beginning) (region-end) 2))

;; (defun ew-outdent()
;;   "*Unindent code by two spaces."
;;   (interactive)
;;   (indent-rigidly (region-beginning) (region-end) -2))

